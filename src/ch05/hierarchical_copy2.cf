body common control
{
        inputs => { "cfengine_stdlib.cf" };
        bundlesequence => { "copyfiles" };
}

# Use single copy for all files
body agent control
{
        files_single_copy => { ".*" }; 
}

bundle agent copyfiles
{
  vars:
      # Suffixes to try, in most-specific to most-general order. This must include the
      # empty suffix at the end, for the most general file.
      "suffixes"    slist => { ".$(sys.fqhost)", ".$(sys.uqhost)", ".$(sys.domain)", 
                               ".$(sys.flavor)", ".$(sys.ostype)", "" };
      # List of files to copy
      "filestocopy"     slist => { "/etc/hosts", "/etc/motd" };   
      "dirstocopy"      slist => { "$(sys.workdir)/bin", "/usr/local/bin" };
      # Source of the files
      "repo"            string => "/mnt/fileserver/cfengine/files";   
      # Destination for the files
      # Set this to an empty string for a production environment
      # "dest" string => "";
      "dest"            string => "/tmp/testdest";   

  files:
      "$(dest)$(filestocopy)"   
        copy_from => local_dcp("$(repo)$(filestocopy)$(suffixes)");

      "$(dest)$(dirstocopy)"    
        copy_from => local_dcp("$(repo)$(dirstocopy)$(suffixes)"),
        depth_search => recurse("inf");
}
